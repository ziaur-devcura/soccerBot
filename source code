const int motorLpin1 = 2;
const int motorLpin2 = 3;
const int motorRpin1 = 4;
const int motorRpin2 = 5;
const int motorLpwm = 10;
const int motorRpwm = 11;

int motorSpeed = 125;
int turn = 50;

void setup() {
  Serial.begin(9600);
  pinMode(motorLpin1, OUTPUT);
  pinMode(motorLpin2, OUTPUT);
  pinMode(motorRpin1, OUTPUT);
  pinMode(motorRpin2, OUTPUT);
  pinMode(motorLpwm, OUTPUT);
  pinMode(motorRpwm, OUTPUT);
  stopMotors(); // Stop motors initially
}

void loop() {
  String input = readSerialInput();
  
  if (input == "n") {
    stopMotors();
  } else if (input == "F") {
    moveForward();
  } else if (input == "R") {
    moveReverse();
  } else if (input.indexOf("TL") > -1) {
    turnLeft();
  } else if (input.indexOf("TR") > -1) {
    turnRight();
  } else if (input != "") {
    setMotorSpeed(input.toInt());
  }
}

String readSerialInput() {
  String input = "";
  while (Serial.available()) {
    input += (char)Serial.read();
    delay(5);
  }
  return input;
}

void moveForward() {
  analogWrite(motorLpwm, motorSpeed);
  analogWrite(motorRpwm, motorSpeed);
  digitalWrite(motorLpin1, HIGH);
  digitalWrite(motorLpin2, LOW);
  digitalWrite(motorRpin1, HIGH);
  digitalWrite(motorRpin2, LOW);
}

void moveReverse() {
  analogWrite(motorLpwm, motorSpeed);
  analogWrite(motorRpwm, motorSpeed);
  digitalWrite(motorLpin1, LOW);
  digitalWrite(motorLpin2, HIGH);
  digitalWrite(motorRpin1, LOW);
  digitalWrite(motorRpin2, HIGH);
}

void turnLeft() {
  analogWrite(motorLpwm, motorSpeed - turn);
  analogWrite(motorRpwm, motorSpeed + turn);
  digitalWrite(motorLpin1, LOW);
  digitalWrite(motorLpin2, HIGH);
  digitalWrite(motorRpin1, HIGH);
  digitalWrite(motorRpin2, LOW);
}

void turnRight() {
  analogWrite(motorLpwm, motorSpeed + turn);
  analogWrite(motorRpwm, motorSpeed - turn);
  digitalWrite(motorLpin1, HIGH);
  digitalWrite(motorLpin2, LOW);
  digitalWrite(motorRpin1, LOW);
  digitalWrite(motorRpin2, HIGH);
}

void stopMotors() {
  analogWrite(motorLpwm, 0);
  analogWrite(motorRpwm, 0);
  digitalWrite(motorLpin1, HIGH);
  digitalWrite(motorLpin2, HIGH);
  digitalWrite(motorRpin1, HIGH);
  digitalWrite(motorRpin2, HIGH);
}

void setMotorSpeed(int speed) {
  motorSpeed = constrain(speed, 0, 255);
}
